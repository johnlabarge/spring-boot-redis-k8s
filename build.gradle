buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.2.RELEASE")
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

apply plugin: 'groovy'


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'docker'

jar {
    baseName = 'spring-boot-app'
    version  = '0.1.0'
}
project.ext.gcp = "gcr.io/cloudjlb-eventer"


task buildBootImage(type: Docker, dependsOn: build) {
    push = false
    applicationName = jar.baseName
    tag = "${project.ext.gcp}/${applicationName}"
    tagVersion=null
    dockerfile = file('src/main/docker/Dockerfile')
    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}

task deployBootImage(type: Exec, dependsOn: buildBootImage) {
    commandLine "gcloud","docker","--","push", "${buildBootImage.tag}"
}
task npmInstall(type: Exec) {
    workingDir "web-frontend/eventer"
    commandLine  "npm", "install", "."
}
task buildAngular(type: Exec, dependsOn: npmInstall) {
    workingDir "web-frontend/eventer"
    commandLine "ng", "build"
}
task buildFeImage(type: Docker, dependsOn: buildAngular) {
    push = false
    tag = "${project.ext.gcp}/web-frontend"
    dockerfile = file('web-frontend/eventer/Dockerfile')
    doFirst {
        copy {
            from file("web-frontend/eventer/dist")
            into file(stageDir.getPath()+'/dist')
        }
        copy {
            from file("web-frontend/eventer/nginx.conf")
            into stageDir
        }
    }
}
task deployFeImage(type: Exec, dependsOn: buildFeImage) {
    commandLine "gcloud", "docker", "--", "push",  "${buildFeImage.tag}"
}

task buildRedisImage(type: Docker) {
    push = false
    tag = "${project.ext.gcp}/redis-sentinel"
    dockerfile = file("redis-docker/Dockerfile")
    doFirst {
        copy {
            from file("redis-docker/docker-entrypoint.sh")
            into file(stageDir.getPath())
        }
        copy {
            from file("redis-docker/redis.conf")
            into file(stageDir.getPath())
         }
    }
}

task deployRedisImage(type: Exec, dependsOn: buildRedisImage) {
    commandLine "gcloud", "docker", "--", "push", "${buildRedisImage.tag}"
}
task deployment
repositories {
    mavenCentral()
}
targetCompatibility = 1.8
sourceCompatibility = 1.8
dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.data:spring-data-redis"

    compile "biz.paluch.redis:lettuce:3.5.0.Final"
    compile "se.transmode.gradle:gradle-docker:1.2"
    //compile "org.springframework.boot:spring-boot-starter-security"
    //compile "com.google.firebase:firebase-admin:4.0.3"
    compile group: 'com.google.cloud', name: 'google-cloud-datastore', version: '0.8.0-beta'
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

